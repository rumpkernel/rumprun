OUTPUT_ARCH(arm)
ENTRY(_start32)
SECTIONS
{
	. = 1m;
	_begin = . ;
	.text :
	AT (ADDR(.text) & 0x0fffffff)
	{
		*(.multiboot)
		*(.text)
		*(.text.*)
		*(.stub)
		*(.note*)
	}
	_etext = . ;

	.rodata :
	AT (LOADADDR(.text) + (ADDR(.rodata) - ADDR(.text)))
	{
		*(.rodata)
	}

	_data_start = .;
	.data :
	AT (LOADADDR(.text) + (ADDR(.data) - ADDR(.text)))
	{
		*(.data)
	}
	_edata = . ;

	__bss_start = . ;
	.bss :
	AT (LOADADDR(.text) + (ADDR(.bss) - ADDR(.text)))
	{
		*(.bss)
		*(COMMON)
		*(.bootstack)
	}
	_end = . ;
	PROVIDE (end = .) ;
}
